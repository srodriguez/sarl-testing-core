package io.sarl.testing.core

import io.sarl.lang.core.Address
import io.sarl.lang.core.Behavior
import io.sarl.lang.core.Capacity
import io.sarl.lang.core.Event
import io.sarl.lang.core.EventSpaceSpecification
import io.sarl.lang.core.Skill
import io.sarl.lang.core.SpaceID
import io.sarl.sre.capacities.InternalSchedules
import io.sarl.sre.internal.eventguard.BehaviorGuardEvaluatorRegistry
import io.sarl.sre.skills.internal.EventBus
import java.util.UUID
import java.util.function.Supplier
import io.sarl.sre.skills.bic.SchedulesSkill
import io.sarl.core.Logging
import io.sarl.core.Initialize
import static org.mockito.Mockito.*
import org.eclipse.xtend.lib.annotations.Accessors
import io.sarl.core.Behaviors

/** 
 * Agent Harness to unit test behaviors, skill and capacities.
 * 
 * This is <b>not</b> a fully executable agent, but a stub that allows for unit testing only.
 * 
 * In provides the following 'pre-installed' stubs for the built-in capacities
 * 
 * <ul>
 * <li>Logging</li>
 * </ul>
 * 
 * Other BICs must be install before the tests.
 * 
 * @author Sebastian Rodriguez
 * 
 */
agent TestingAgent {
	val sup = new Supplier<InternalSchedules> {

		def get : InternalSchedules {
			new SchedulesSkill(new TestingExecutorService)
		}

	}
	val eventBus = new EventBus(sup, new BehaviorGuardEvaluatorRegistry)

	val defaultAddress = new Address(new SpaceID(this.ID, UUID.randomUUID, EventSpaceSpecification), this.ID)


	//BICs for support
	
	@Accessors(PUBLIC_GETTER) val logging = spy(LoggingStub)
	@Accessors(PUBLIC_GETTER) val behaviors = spy(BehaviorsStub)
	
	new() {
		super(UUID.randomUUID, UUID.randomUUID)
		setSkill(logging, Logging)
		setSkill(behaviors, Behaviors)
		doNothing.when(this.behaviors).wake(any(Event))
	}

	def getDefaultAddress : Address {
		this.defaultAddress
	}

	def withSkill(s : Skill, cap : Class<? extends Capacity>) : TestingAgent {
		setSkill(s, cap)
		this
	}

	def registerBehavior(b : Behavior) {
		registerBehavior(b,[])
	}

	def registerBehavior(b : Behavior, params: Object*) {
		this.eventBus.register(b, null, null)
		this.eventBus.immediateDispatchTo(b, new Initialize(this.ID,params), true)
	}
	def perceive(e : Event) {
		this.eventBus.immediateDispatch(e, true)
	}

}
